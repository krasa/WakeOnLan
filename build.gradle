plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.4'
}

javafx {
    version = "14"
    modules = ['javafx.controls', 'javafx.fxml','javafx.graphics']
}
jar {
    enabled = true
}

group = 'krasa.wakeonlan'
version = '0.2'
sourceCompatibility = '11'


repositories {
    mavenCentral()
}

project.ext.buildDate = new Date()
dependencies {
    // https://mvnrepository.com/artifact/net.rgielen/javafx-weaver-spring-boot-starter
    implementation group: 'net.rgielen', name: 'javafx-weaver-spring-boot-starter', version: '1.3.0'
//     https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    // https://mvnrepository.com/artifact/com.hierynomus/sshj
    implementation group: 'com.hierynomus', name: 'sshj', version: '0.30.0'
    
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}


application {
    mainClassName = "krasa.wakeup.WakeOnLanApplication"
    applicationName = 'Probouzeƒç PC'
}
//https://github.com/beryx-gist/badass-runtime-example-javafx
runtime {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        noConsole = false
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Nimblsoft']
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
    
   
    targetPlatform("win") {
        jdkHome = 'C:\\Users\\i7\\.jdks\\openjdk-15.0.1'
        
    }
}

